```
tip: 165
title: TRC-165 Standard Interface Detection In Contract
author: timothychung timothychungkitwai@gmail.com
discussions to: https://github.com/tronprotocol/tips/issues/200
status: Draft
type: Standards Track
category : TRC
created: 2020-11-20
```

## Simple Summary

A standard method to publish and detect what interfaces a smart contract have implemented.

## Abstract

This TIP is compatible with [EIP-165](https://github.com/ethereum/EIPs/blob/master/EIPS/eip-165.md), standardize the following in TRON version:

1. How interfaces are identified

2. How a contract will publish the interfaces it implements

3. How to detect if a contract implements ERC-165

4. How to detect if a contract implements any given interface 


## Motivation

We don't have a standard interface to check the contract if support some "standard interfaces" like TRC-20 token interface. It's hard to interact with other contracts. For the purpose of the interactivity. This proposal standardizes the concept and identification of interfaces.

## Specification

### How Interfaces are Identified

For this standard, an *interface* is a set of [function selectors as defined by the Ethereum ABI](https://solidity.readthedocs.io/en/develop/abi-spec.html#function-selector). This a subset of [Solidity’s concept of interfaces](https://solidity.readthedocs.io/en/develop/abi-spec.html) and the `interface` keyword definition which also defines return types, mutability and events.

We define the interface identifier as the XOR of all function selectors in the interface. This code example shows how to calculate an interface identifier:

```solidity
pragma solidity ^0.4.20;

interface Solidity101 {
    function hello() external pure;
    function world(int) external pure;
}

contract Selector {
    function calculateSelector() public pure returns (bytes4) {
        Solidity101 i;
        return i.hello.selector ^ i.world.selector;
    }
}
```

Note: interfaces do not permit optional functions, therefore, the interface identity will not include them.

### How a Contract will Publish the Interfaces it Implements

A contract that is compliant with TRC-165 shall implement the following interface (referred as `TRC165.sol`):

```solidity
pragma solidity ^0.4.20;

interface TRC165 {
    /// @notice Query if a contract implements an interface
    /// @param interfaceID The interface identifier, as specified in TRC-165
    /// @dev Interface identification is specified in TRC-165.
    ///  uses less than 30,000 energy.
    /// @return `true` if the contract implements `interfaceID` and
    ///  `interfaceID` is not 0xffffffff, `false` otherwise
    function supportsInterface(bytes4 interfaceID) external view returns (bool);
}
```

The interface identifier for this interface is `0x01ffc9a7`. You can calculate this by running ` bytes4(keccak256(‘supportsInterface(bytes4)’));` or using the `Selector` contract above.

Therefore the implementing contract will have a `supportsInterface` function that returns:

- `true` when `interfaceID` is `0x01ffc9a7` (TIP165 interface)
- `false` when `interfaceID` is `0xffffffff`
- `true` for any other `interfaceID` this contract implements
- `false` for any other `interfaceID`

This function must return a bool and use at most 30,000 energy.

Implementation note, there are several logical ways to implement this function. Please see the example implementations and the discussion on energy usage.

### How to Detect if a Contract Implements TRC-165

1. The source contract makes a `STATICCALL` to the destination address with input data: `0x01ffc9a701ffc9a700000000000000000000000000000000000000000000000000000000 ` and energy 30,000. This corresponds to `contract.supportsInterface(0x01ffc9a7)`.
2. If the call fails or return false, the destination contract does not implement TRC-165.
3. If the call returns true, a second call is made with input data `0x01ffc9a7ffffffff00000000000000000000000000000000000000000000000000000000`.
4. If the second call fails or returns true, the destination contract does not implement TRC-165.
5. Otherwise it implements TRC-165.

### How to Detect if a Contract Implements any Given Interface

1. If you are not sure if the contract implements TRC-165, use the above procedure to confirm.
2. If it does not implement TRC-165, then you will have to see what methods it uses the old-fashioned way.
3. If it implements TRC-165 then just call `supportsInterface(interfaceID)` to determine if it implements an interface you can use.

## Rationale

We tried to keep this specification as simple as possible. This implementation is also compatible with the current Solidity version.

## Backwards Compatibility

The mechanism described above (with `0xffffffff`) should work with most of the contracts previous to this standard to determine that they do not implement TRC-165.

## Test Cases

Following is a contract that detects which interfaces other contracts implement.

```solidity
pragma solidity ^0.4.20;

contract TRC165Query {
    bytes4 constant InvalidID = 0xffffffff;
    bytes4 constant TRC165ID = 0x01ffc9a7;

    function doesContractImplementInterface(address _contract, bytes4 _interfaceId) external view returns (bool) {
        uint256 success;
        uint256 result;

        (success, result) = noThrowCall(_contract, TRC165ID);
        if ((success==0)||(result==0)) {
            return false;
        }

        (success, result) = noThrowCall(_contract, InvalidID);
        if ((success==0)||(result!=0)) {
            return false;
        }

        (success, result) = noThrowCall(_contract, _interfaceId);
        if ((success==1)&&(result==1)) {
            return true;
        }
        return false;
    }

    function noThrowCall(address _contract, bytes4 _interfaceId) constant internal returns (uint256 success, uint256 result) {
        bytes4 trc165ID = TRC165ID;

        assembly {
                let x := mload(0x40)               // Find empty storage location using "free memory pointer"
                mstore(x, trc165ID)                // Place signature at beginning of empty storage
                mstore(add(x, 0x04), _interfaceId) // Place first argument directly next to signature

                success := staticcall(
                                    30000,         // 30k energy
                                    _contract,     // To addr
                                    x,             // Inputs are stored at location x
                                    0x24,          // Inputs are 36 bytes long
                                    x,             // Store output over input (saves space)
                                    0x20)          // Outputs are 32 bytes long

                result := mload(x)                 // Load the result
        }
    }
}
```

## Implementation

This approach uses a `view` function implementation of `supportsInterface`. The `TRC165MappingImplementation` contract is generic and reusable.

```solidity
pragma solidity ^0.4.20;

import "./TRC165.sol";

contract TRC165MappingImplementation is TRC165 {
    /// @dev You must not set element 0xffffffff to true
    mapping(bytes4 => bool) internal supportedInterfaces;

    function TRC165MappingImplementation() internal {
        supportedInterfaces[this.supportsInterface.selector] = true;
    }

    function supportsInterface(bytes4 interfaceID) external view returns (bool) {
        return supportedInterfaces[interfaceID];
    }
}

interface Simpson {
    function is2D() external returns (bool);
    function skinColor() external returns (string);
}

contract Lisa is TRC165MappingImplementation, Simpson {
    function Lisa() public {
        supportedInterfaces[this.is2D.selector ^ this.skinColor.selector] = true;
    }

    function is2D() external returns (bool){}
    function skinColor() external returns (string){}
}
```

Following is a `pure` function implementation of `supportsInterface`.

```solidity
pragma solidity ^0.4.20;

import "./TRC165.sol";

interface Simpson {
    function is2D() external returns (bool);
    function skinColor() external returns (string);
}

contract Homer is TRC165, Simpson {
    function supportsInterface(bytes4 interfaceID) external view returns (bool) {
        return
          interfaceID == this.supportsInterface.selector || // TRC165
          interfaceID == this.is2D.selector
                         ^ this.skinColor.selector; // Simpson
    }

    function is2D() external returns (bool){}
    function skinColor() external returns (string){}
}
```

With three or more supported interfaces (including TRC165 itself as a required supported interface), the mapping approach (in every case) costs less energy than the pure approach (at worst case).

## Version history

- PR 1640, finalized 2019-01-23 – This corrects the noThrowCall test case to use 36 bytes rather than the previous 32 bytes. The previous code was an error that still silently worked in Solidity 0.4.x but which was broken by new behavior introduced in Solidity 0.5.0. This change was discussed at [#1640](https://github.com/ethereum/EIPs/pull/1640).
- EIP 165, finalized 2018-04-20 – Original published version.

## Copyright

Copyright and related rights waived via [CC0](https://creativecommons.org/publicdomain/zero/1.0/).

## Citation

Please cite this document as:

[Christian Reitwießner](mailto:chris@ethereum.org), [Nick Johnson](mailto:nick@ethereum.org), [Fabian Vogelsteller](mailto:fabian@lukso.network), [Jordi Baylina](mailto:jordi@baylina.cat), [Konrad Feldmeier](mailto:konrad.feldmeier@brainbot.com), [William Entriken](mailto:github.com@phor.net), "EIP-165: ERC-165 Standard Interface Detection," *Ethereum Improvement Proposals*, no. 165, January 2018. [Online serial]. Available: https://eips.ethereum.org/EIPS/eip-165.